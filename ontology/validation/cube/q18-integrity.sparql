#  if A qb:slice B and B qb:observation C then C qb:dataSet A
PREFIX rdf:            <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:           <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:           <http://www.w3.org/2004/02/skos/core#>
PREFIX qb:             <http://purl.org/linked-data/cube#>
PREFIX xsd:            <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:            <http://www.w3.org/2002/07/owl#>
PREFIX eg:             <http://example.com/abbrv-cube/>
prefix cex: 		   <http://purl.org/weso/ontology/computex#> 

CONSTRUCT {
 _:error a cex:Error .
 _:error cex:errorParam 
	[cex:name "dataset"; cex:value ?dataset ]  ,
	[cex:name "slice"; cex:value ?slice ]  ,
	[cex:name "obs"; cex:value ?obs ]  
.
 _:error cex:msg "if A qb:slice B and B qb:observation C then C qb:dataSet A" .
}
WHERE {
    ?dataset qb:slice       ?slice .
    ?slice   qb:observation ?obs .
    FILTER NOT EXISTS { ?obs qb:dataSet ?dataset . }
}
