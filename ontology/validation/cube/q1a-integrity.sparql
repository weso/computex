PREFIX rdf:            <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:           <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:           <http://www.w3.org/2004/02/skos/core#>
PREFIX qb:             <http://purl.org/linked-data/cube#>
PREFIX xsd:            <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:            <http://www.w3.org/2002/07/owl#>
PREFIX eg:             <http://example.com/abbrv-cube/>
prefix cex: 		   <http://purl.org/weso/ontology/computex#> 

# Every observation has a unique associated data set (true if there is a problem)

CONSTRUCT {
 _:error a cex:Error .
 _:error cex:errorParam 
    [cex:name "obs"; cex:value ?obs], 
	[cex:name "dataset1"; cex:value ?dataset1], 
	[cex:name "dataset2"; cex:value ?dataset2] .
 _:error cex:msg "Observation must have only one data set" .
} WHERE {
    # Check has just one data set
    ?obs a qb:Observation ;
         qb:dataSet ?dataset1, ?dataset2 .
    FILTER (?dataset1 != ?dataset2)
}
