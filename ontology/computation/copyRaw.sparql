# compute copy-Raw
prefix country:      <http://data.webfoundation.org/webindex/v2013/country/> 
prefix dataset:      <http://data.webfoundation.org/webindex/v2013/dataset/> 
prefix indicator:    <http://data.webfoundation.org/webindex/v2013/indicator/> 
prefix obs:          <http://data.webfoundation.org/webindex/v2013/observation/> 
prefix organization: <http://data.webfoundation.org/webindex/v2013/organization> 

prefix afn:			 <http://jena.hpl.hp.com/ARQ/function#>
prefix cex:          <http://purl.org/weso/ontology/computex#> 
prefix owl:          <http://www.w3.org/2002/07/owl#> 
prefix qb:           <http://purl.org/linked-data/cube#>
prefix rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:         <http://www.w3.org/2000/01/rdf-schema#> 
prefix sdmx-code:  <http://purl.org/linked-data/sdmx/2009/code#> 
prefix sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#> 
prefix wi-onto:      <http://data.webfoundation.org/webindex/ontology/> 
prefix xml:          <http://www.w3.org/XML/1998/namespace> 
prefix xsd:          <http://www.w3.org/2001/XMLSchema#> 

CONSTRUCT {
 ?sliceToCopy qb:observation [ 
       a qb:Observation ;
       cex:value ?value ;
       qb:dataSet ?dataSetToCopy ;
       cex:indicator ?indicator ;
       wi-onto:ref-area ?area ;
	   wi-onto:ref-year ?year ;
	   sdmx-concept:obsStatus cex:Imputed ;
	   cex:computation [ a cex:Copy ;
		                   cex:observation ?obsRaw 
						 ] 
  ]
} WHERE {
 ?dataSetToCopy a qb:DataSet ;
          cex:computation [ a cex:ImputeDataSet ;
	                        cex:method cex:CopyRaw; 
						    cex:dataSet ?dataset ;
					      ] ;
		  qb:slice ?sliceToCopy .
 ?dataset qb:slice ?sliceRaw .
         
 ?sliceRaw cex:indicator ?indicator ;
	       wi-onto:ref-year ?year ;
	       qb:observation ?obsRaw .

 ?sliceToCopy cex:indicator ?indicator ;
           wi-onto:ref-year ?year .
           
 ?obsRaw cex:value ?value ;
         wi-onto:ref-area ?area ;
         .
}
