# adjust values incrementing one dimension
prefix country:      <http://data.webfoundation.org/webindex/v2013/country/> 
prefix dataset:      <http://data.webfoundation.org/webindex/v2013/dataset/> 
prefix indicator:    <http://data.webfoundation.org/webindex/v2013/indicator/> 
prefix obs:          <http://data.webfoundation.org/webindex/v2013/observation/> 
prefix organization: <http://data.webfoundation.org/webindex/v2013/organization> 

prefix afn:			 <http://jena.hpl.hp.com/ARQ/function#>
prefix cex:          <http://purl.org/weso/ontology/computex#> 
prefix owl:          <http://www.w3.org/2002/07/owl#> 
prefix qb:           <http://purl.org/linked-data/cube#>
prefix rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:         <http://www.w3.org/2000/01/rdf-schema#> 
prefix sdmx-code:  <http://purl.org/linked-data/sdmx/2009/code#> 
prefix sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#> 
prefix wf-onto:      <http://data.webfoundation.org/ontology/> 
prefix xml:          <http://www.w3.org/XML/1998/namespace> 
prefix xsd:          <http://www.w3.org/2001/XMLSchema#> 

CONSTRUCT {
 _:obs a qb:Observation ;
       cex:value ?adjusted ;
       qb:dataSet ?dataSetToCopy ;
       cex:indicator ?indicator ;
	   wf-onto:ref-year ?year ;
	   wf-onto:ref-area ?area ;
	   sdmx-concept:obsStatus cex:Adjusted ;
	   cex:computation [ a cex:Adjust ;
	                       cex:reason "Adjust values by an increment"@en ;
		                   cex:observation ?obsRaw ;
		                   cex:increment ?increm
						 ] ;
	.
 ?sliceToCopy qb:observation _:obs .
} WHERE {

 ?dataSetToCopy a qb:DataSet ;
      cex:computation [ a cex:AdjustDataSet ;
						cex:dataSet ?datasetFrom ;
						cex:increment ?increm ;
					  ] ;
      qb:slice ?sliceToCopy .

 ?datasetFrom qb:slice ?sliceFrom .
         
 ?sliceFrom cex:indicator ?indicator ;
	        qb:observation ?obsRaw ;
	        .
 ?sliceToCopy ?dim ?valueDim ;
            cex:indicator ?indicator .

 ?obsRaw cex:value ?value ;
         cex:indicator ?indicator ;
         wf-onto:ref-area ?area ;
         wf-onto:ref-year ?year ;
         .
         
 BIND(?value + ?increm as ?adjusted)
}