# filter values from some dimension
prefix country:      <http://data.webfoundation.org/webindex/v2013/country/> 
prefix dataset:      <http://data.webfoundation.org/webindex/v2013/dataset/> 
prefix indicator:    <http://data.webfoundation.org/webindex/v2013/indicator/> 
prefix obs:          <http://data.webfoundation.org/webindex/v2013/observation/> 
prefix organization: <http://data.webfoundation.org/webindex/v2013/organization> 

prefix afn:                         <http://jena.hpl.hp.com/ARQ/function#>
prefix cex:          <http://purl.org/weso/ontology/computex#> 
prefix owl:          <http://www.w3.org/2002/07/owl#> 
prefix qb:           <http://purl.org/linked-data/cube#>
prefix rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:         <http://www.w3.org/2000/01/rdf-schema#> 
prefix sdmx-code:  <http://purl.org/linked-data/sdmx/2009/code#> 
prefix sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#> 
prefix wf-onto:      <http://data.webfoundation.org/ontology/> 
prefix xml:          <http://www.w3.org/XML/1998/namespace> 
prefix xsd:          <http://www.w3.org/2001/XMLSchema#> 

CONSTRUCT {
 _:obs a qb:Observation ;
           ?dim ?valueDim ;
       cex:value ?value ;
       qb:dataSet ?dataSetToCopy ;
       cex:indicator ?indicator ;
           wf-onto:ref-year ?year ;
           sdmx-concept:obsStatus cex:Imputed ;
           cex:computation [ a cex:Filter ;
                                   cex:observation ?obsRaw  ;
                                                   cex:reason "Filter observations which hava a given dimension"@en ;
                                                   cex:filter [
                                                       a cex:FilterDefinition ;
                                                      cex:dimension ?dim ;
                                                      cex:value ?valueDim 
                                                   ]
                                                 ] ;
        .
 ?sliceToCopy qb:observation _:obs .
} WHERE {
 ?dataSetToCopy a qb:DataSet ;
                    cex:computation [ a cex:FilterDataSet ;
                                    cex:reason "Filter the values of a dataset"@en ;
                                                    cex:dataSet ?dataset ;
                                                    cex:filter ?filter 
                                              ] ;
          qb:slice ?sliceToCopy .

 ?filter a cex:FilterDefinition ;
                 cex:dimension ?dim ;
                 cex:value ?valueDim  .

 ?dataset qb:slice ?sliceFrom .
         
 ?sliceFrom cex:indicator ?indicator ;
                wf-onto:ref-year ?year ;
                qb:observation ?obsRaw ;
               .
 ?sliceToCopy wf-onto:ref-year ?year .

 ?obsRaw cex:value ?value ;
         ?dim ?valueDim .

}
