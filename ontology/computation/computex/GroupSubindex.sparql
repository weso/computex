# weighted values from some weight schema
prefix country:      <http://data.webfoundation.org/webindex/v2013/country/> 
prefix dataset:      <http://data.webfoundation.org/webindex/v2013/dataset/> 
prefix indicator:    <http://data.webfoundation.org/webindex/v2013/indicator/> 
prefix obs:          <http://data.webfoundation.org/webindex/v2013/observation/> 
prefix organization: <http://data.webfoundation.org/webindex/v2013/organization> 

prefix afn:			 <http://jena.hpl.hp.com/ARQ/function#>
prefix cex:          <http://purl.org/weso/ontology/computex#> 
prefix owl:          <http://www.w3.org/2002/07/owl#> 
prefix qb:           <http://purl.org/linked-data/cube#>
prefix rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:         <http://www.w3.org/2000/01/rdf-schema#> 
prefix sdmx-code:  <http://purl.org/linked-data/sdmx/2009/code#> 
prefix sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#> 
prefix wf-onto:      <http://data.webfoundation.org/ontology/> 
prefix xml:          <http://www.w3.org/XML/1998/namespace> 
prefix xsd:          <http://www.w3.org/2001/XMLSchema#> 

CONSTRUCT {
 ?sliceToCopy qb:observation [
       a qb:Observation ;
       cex:value ?mean ;
       wf-onto:ref-year ?year ;
       qb:dataSet ?dataSetToCopy ;
       cex:indicator ?component ;
	   ?dimension ?valueDim ;
	   sdmx-concept:obsStatus cex:Grouped ;
	   qb:dataset ?dataSetToCopy ;
	   cex:computation [ a cex:GroupMean ;
	                       cex:reason "Group subindexes"@en ;
						   cex:component ?lsObs ] ;
  ]
} WHERE {
 ?dataSetToCopy a qb:DataSet ;
      cex:computation ?comp ;
      qb:slice ?sliceToCopy .
      
 ?comp a cex:GroupSubindex  ;
	   cex:dataSet ?dataset ;
	   cex:component ?component ;
	   cex:dimension ?dimension
	   .
	   
 ?sliceToCopy cex:indicator ?component ;
              wf-onto:ref-year ?year .

 { SELECT (AVG(?value) as ?mean) 
          (GROUP_CONCAT(?obs) as ?lsObs) 
          ?dataset 
          ?component 
          ?dimension 
          ?valueDim WHERE {
    
    ?component cex:element ?element .
 
    ?dataset qb:slice ?sliceFrom .
 
    ?sliceFrom cex:indicator ?element ; 
               qb:observation ?obs .
    
    ?obs cex:value ?value ;
         ?dimension ?valueDim .
         
  } GROUP BY ?dataset ?component ?dimension ?valueDim
 }
}
