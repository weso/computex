# adjust values from some dimension
prefix country:      <http://data.webfoundation.org/webindex/v2013/country/> 
prefix dataset:      <http://data.webfoundation.org/webindex/v2013/dataset/> 
prefix indicator:    <http://data.webfoundation.org/webindex/v2013/indicator/> 
prefix obs:          <http://data.webfoundation.org/webindex/v2013/observation/> 
prefix organization: <http://data.webfoundation.org/webindex/v2013/organization> 

prefix afn:			 <http://jena.hpl.hp.com/ARQ/function#>
prefix cex:          <http://purl.org/weso/ontology/computex#> 
prefix owl:          <http://www.w3.org/2002/07/owl#> 
prefix qb:           <http://purl.org/linked-data/cube#>
prefix rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:         <http://www.w3.org/2000/01/rdf-schema#> 
prefix sdmx-code:  <http://purl.org/linked-data/sdmx/2009/code#> 
prefix sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#> 
prefix wf-onto:      <http://data.webfoundation.org/ontology/> 
prefix xml:          <http://www.w3.org/XML/1998/namespace> 
prefix xsd:          <http://www.w3.org/2001/XMLSchema#> 

CONSTRUCT {
 _:obs a qb:Observation ;
       cex:value ?adjusted ;
       cex:indicator ?indicator ;
       qb:dataSet ?dataSetToCopy ;
	   ?dim ?valueDim ;
	   wf-onto:ref-area ?area ;
	   sdmx-concept:obsStatus cex:Adjusted ;
	   cex:computation [ a cex:Adjust ;
		                   cex:observation ?obsRaw ;
		                   cex:increment ?increm
						 ] ;
	.
 ?sliceToCopy qb:observation _:obs .
} WHERE {

 ?dataSetToCopy a qb:DataSet ;
      cex:computation [ a cex:AdjustDataSet ;
						cex:dataSet ?datasetFrom ;
						cex:increment ?increm ;
						cex:dimension ?dim ;
						cex:value ?valueDim 
					  ] ;
      qb:slice ?sliceToCopy .

 ?datasetFrom qb:slice ?sliceFrom .
         
 ?sliceFrom cex:indicator ?indicator ;
	        ?dim ?valueDim ;
	        qb:observation ?obsRaw ;
	        .
 ?sliceToCopy ?dim ?valueDim ;
            cex:indicator ?indicator ;
            wf-onto:ref-year ?year .

 ?obsRaw cex:value ?value ;
         wf-onto:ref-year ?year ;
         cex:indicator ?indicator ;
         ?dim ?valueDim ;
         wf-onto:ref-area ?area ;
         .
         
 BIND(?value + ?increm as ?adjusted)
}