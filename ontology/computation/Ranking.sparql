# Ranking
prefix country:      <http://data.webfoundation.org/webindex/v2013/country/> 
prefix dataset:      <http://data.webfoundation.org/webindex/v2013/dataset/> 
prefix indicator:    <http://data.webfoundation.org/webindex/v2013/indicator/> 
prefix obs:          <http://data.webfoundation.org/webindex/v2013/observation/> 
prefix organization: <http://data.webfoundation.org/webindex/v2013/organization> 

prefix afn:			 <http://jena.hpl.hp.com/ARQ/function#>
prefix cex:          <http://purl.org/weso/ontology/computex#> 
prefix owl:          <http://www.w3.org/2002/07/owl#> 
prefix qb:           <http://purl.org/linked-data/cube#>
prefix rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:         <http://www.w3.org/2000/01/rdf-schema#> 
prefix sdmx-code:  <http://purl.org/linked-data/sdmx/2009/code#> 
prefix sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#> 
prefix wi-onto:      <http://data.webfoundation.org/webindex/ontology/> 
prefix xml:          <http://www.w3.org/XML/1998/namespace> 
prefix xsd:          <http://www.w3.org/2001/XMLSchema#> 

CONSTRUCT {
 ?sliceToCopy qb:observation 
   [ a qb:Observation; 
     cex:computation 
      [ a cex:Rank ;
        cex:slice ?sliceToRank;
        cex:dimension ?dim 
      ] ;
     qb:dataSet ?dataSetToCopy ;
     wi-onto:ref-year ?year ;
     cex:indicator ?indicator ;
     cex:value ?ranking ;
     ?dim ?valueDim 
   ] .
} WHERE {

 ?dataSetToCopy a qb:DataSet ;
      cex:computation ?comp ;
      qb:slice ?sliceToCopy .

 ?comp a cex:Ranking ;
       cex:slice ?sliceToRank ;
       cex:dimension ?dim
	 .      
	   
 ?sliceToCopy cex:indicator ?component .
 
 ?sliceToRank qb:observation ?obsToRank ;
              cex:indicator ?component ;
              .
 ?obsToRank ?dim ?valueDim ;
            cex:indicator ?component ;
            wi-onto:ref-year ?year ;
            cex:value ?valueToRank .
      
 { SELECT (COUNT(*) as ?ranking) ?sliceToRank ?obsToRank ?valueToRank WHERE {
     ?sliceToRank qb:observation ?obs ;
         qb:observation ?obsToRank .

     ?obs cex:value ?value ;
          .
     ?obsToRank cex:value ?valueToRank ;
          .
     
   FILTER( ?valueToRank <= ?value )
  } GROUP BY ?obsToRank ?valueToRank ?sliceToRank 
 }    
}
