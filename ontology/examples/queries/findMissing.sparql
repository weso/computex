# find Missing observations
prefix country:      <http://data.webfoundation.org/webindex/v2013/country/> 
prefix dataset:      <http://data.webfoundation.org/webindex/v2013/dataset/> 
prefix indicator:    <http://data.webfoundation.org/webindex/v2013/indicator/> 
prefix obs:          <http://data.webfoundation.org/webindex/v2013/observation/> 
prefix organization: <http://data.webfoundation.org/webindex/v2013/organization> 

prefix afn:			 <http://jena.hpl.hp.com/ARQ/function#>
prefix cex:          <http://purl.org/weso/ontology/computex#> 
prefix owl:          <http://www.w3.org/2002/07/owl#> 
prefix qb:           <http://purl.org/linked-data/cube#>
prefix rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:         <http://www.w3.org/2000/01/rdf-schema#> 
prefix wi-onto:      <http://data.webfoundation.org/webindex/ontology/> 
prefix xml:          <http://www.w3.org/XML/1998/namespace> 
prefix xsd:          <http://www.w3.org/2001/XMLSchema#> 

SELECT * WHERE {
 ?obs1 wi-onto:ref-area ?c ;
       qb:dataSet ?dataset ;
       wi-onto:sheet-type wi-onto:Raw ;
       wi-onto:ref-year ?year1 .
 ?obs3 wi-onto:ref-area ?c ;
       qb:dataSet ?dataset ;
       wi-onto:sheet-type wi-onto:Raw ;
       wi-onto:ref-year ?year3 .
  FILTER (?year3 = ?year1 + 2) 
  FILTER NOT EXISTS {
    ?obs2 wi-onto:ref-area ?c ;
         qb:dataSet ?dataset ;
         wi-onto:ref-year ?year2 .
         FILTER (?year2 = ?year1 + 1)
  }
}