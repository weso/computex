# weighted mean 
prefix country:      <http://data.webfoundation.org/webindex/v2013/country/> 
prefix dataset:      <http://data.webfoundation.org/webindex/v2013/dataset/> 
prefix indicator:    <http://data.webfoundation.org/webindex/v2013/indicator/> 
prefix obs:          <http://data.webfoundation.org/webindex/v2013/observation/> 
prefix organization: <http://data.webfoundation.org/webindex/v2013/organization> 

prefix afn:			 <http://jena.hpl.hp.com/ARQ/function#>
prefix cex:          <http://purl.org/weso/ontology/computex#> 
prefix owl:          <http://www.w3.org/2002/07/owl#> 
prefix qb:           <http://purl.org/linked-data/cube#>
prefix rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:         <http://www.w3.org/2000/01/rdf-schema#> 
prefix sdmx-code:  <http://purl.org/linked-data/sdmx/2009/code#> 
prefix sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#> 
prefix wi-onto:      <http://data.webfoundation.org/webindex/ontology/> 
prefix xml:          <http://www.w3.org/XML/1998/namespace> 
prefix xsd:          <http://www.w3.org/2001/XMLSchema#> 

CONSTRUCT {
 _:obs a qb:Observation ;
       cex:value ?weightedMean ;
       cex:indicator ?component ;
	   ?dim ?valueDim ;
	   sdmx-concept:obsStatus cex:Grouped ;
	   qb:dataset ?dataSetToCopy ;
	   cex:computation [ a cex:WeightedMean ;
						   cex:component ?lsObs ] ;
	.
 ?sliceToCopy qb:observation _:obs .

} WHERE {
 ?dataSetToCopy a qb:DataSet ;
      cex:computation ?comp ;
      qb:slice ?sliceToCopy .

 ?comp a cex:WeightedMean ;
	  cex:dataSet ?dataset ; # dataset:SubindexGrouped ;
	  cex:component ?component ; # index:index ;
	  cex:dimension ?dim ; # wi-onto:ref-area ;
	  cex:weightSchema ?weightSchema ;
	 .      
	   
 ?sliceToCopy cex:indicator ?component .

 { SELECT (SUM(?product) as ?weightedMean) 
          (GROUP_CONCAT(?obs) as ?lsObs) 
          ?dataset 
          ?component 
          ?dim
          ?valueDim 
          WHERE {
    
    ?component cex:element ?element .
    ?weightSchema cex:weight [ cex:element ?element ; cex:value ?weight ] .
 
    ?dataset qb:slice ?sliceFrom .
 
    ?sliceFrom cex:indicator ?element ; 
               qb:observation ?obs .
    
    ?obs cex:value ?value ;
         ?dim ?valueDim  .

    BIND(?value * ?weight as ?product)
         
  } GROUP BY ?dataset ?component ?dim ?valueDim ?area
 }


}
